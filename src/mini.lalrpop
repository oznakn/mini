grammar;

use std::str::FromStr;

use crate::ast;

match {
    ",",
    ".",
    ";",
    ":",
    "=",
    "+",
    "-",
    "!",
    "*",
    "/",
    "%",
    "==",
    "===",
    "!=",
    "!==",
    "<",
    "<=",
    ">",
    ">=",
    "&&",
    "||",
    "{",
    "}",
    "(",
    ")",
    "[",
    "]",
    "let",
    "const",
    "string",
    "number",

    "true",
    "false",
    "undefined",
    "null",

    "void",
    "function",
    "export",
    "import",
    "from",

    r"[0-9]+",
    r"[0-9]+\.[0-9]+",
    r"[a-zA-Z][a-zA-Z0-9]*",
    r"'[^\n\r']*'",
    r"`[^\n\r`]*`",

    // Skip whitespace and comments
    r"\s*" => { },
    r"//[^\n\r]*[\n\r]*" => { }, // `// comment`
    r"/\*([^\*]*\*+[^\*/])*([^\*]*\*+|[^\*])*\*/" => { }, // `/* comment */`
}

CommaList<T>: Vec<T> = {
    <items: (<T> ",")*> <last: T?> => {
        let mut items = items;
        items.extend(last);
        items
    }
};

pub Program: ast::Program<'input> =
    <statements:StatementList> => ast::Program {
        statements,
    };

StatementList = Statement*;

Body = "{" <StatementList> "}";

Statement: ast::Statement<'input> = {
    <expression:Expression> ";" => ast::Statement::ExpressionStatement { expression },
    ";" => ast::Statement::EmptyStatement,
    DefinitionStatement,
    FunctionStatement,
};

FunctionReturnKind: ast::VariableKind = {
    "void" => ast::VariableKind::Undefined,
    <kind:VariableKind> => kind,
};

FunctionStatement: ast::Statement<'input> = {
    "function" <identifier:Identifier> "(" <parameters:CommaList<VariableDefinition>> ")" <return_kind:(":" <FunctionReturnKind>)?> <statements:Body> => ast::Statement::FunctionStatement {
        variable: ast::VariableDefinition {
            identifier,
            kind: Some(ast::VariableKind::Function {
                return_kind: Box::new(return_kind.or_else(|| Some(ast::VariableKind::Undefined)).unwrap()),
                parameters: parameters
                            .iter()
                            .map(|parameter| parameter.kind.as_ref().unwrap().clone())
                            .collect(),
            }),
            is_writable: false,
        },
        parameters,
        statements,
    },
};

DefinitionStatementPrefix = {
    "let",
    "const",
};

DefinitionStatement: ast::Statement<'input> = {
    <prefix:DefinitionStatementPrefix> <definition:VariableDefinitionOptionalKind> <e:("=" <Expression>)?> ";" => {
        let mut definition = definition;
        definition.is_writable = prefix == "let";

        ast::Statement::DefinitionStatement {
            variable: definition,
            expression: e,
        }
    },
}

Expression: ast::Expression<'input> = {
    AssignmentExpression,
    BinaryExpression,
};

AssignmentExpression: ast::Expression<'input> =
    <identifier:VariableIdentifier> "=" <e:Expression> => ast::Expression::AssignmentExpression {
        identifier,
        expression: Box::new(e),
    };

Term<L, O, R>: ast::Expression<'input> = {
    <left:L> <operator:O> <right:R> => ast::Expression::BinaryExpression {
        left: Box::new(left),
        operator,
        right: Box::new(right),
    },
    R,
};

BinaryExpression = LogicalTerm4;

LogicalTerm4 = Term<LogicalTerm4, LogicalOperator4, LogicalTerm3>;
LogicalTerm3 = Term<LogicalTerm3, LogicalOperator3, LogicalTerm2>;
LogicalTerm2 = Term<LogicalTerm2, LogicalOperator2, LogicalTerm1>;
LogicalTerm1 = Term<LogicalTerm1, LogicalOperator1, AddTerm>;
AddTerm = Term<AddTerm, AddOperator, MulTerm>;
MulTerm = Term<MulTerm, MulOperator, Factor>;

Factor: ast::Expression<'input> = {
    <operator:UnaryOperator> <e:Factor> => ast::Expression::UnaryExpression {
        operator,
        expression: Box::new(e),
    },
    <l1:@L> <value:Constant> <l2:@R> => ast::Expression::ConstantExpression { location: (l1, l2), value },
    <identifier:VariableIdentifier> => ast::Expression::VariableExpression { identifier },
    <identifier:VariableIdentifier> "(" <arguments:CommaList<Expression>> ")" => ast::Expression::CallExpression { identifier, arguments },
    "(" <e:Expression> ")" => e,
};

UnaryOperator: ast::UnaryOperator = {
    "+" => ast::UnaryOperator::Positive,
    "-" => ast::UnaryOperator::Negative,
    "!" => ast::UnaryOperator::Not,
};

AddOperator: ast::BinaryOperator = {
    "+" => ast::BinaryOperator::Addition,
    "-" => ast::BinaryOperator::Subtraction,
};

MulOperator: ast::BinaryOperator = {
    "*" => ast::BinaryOperator::Multiplication,
    "/" => ast::BinaryOperator::Division,
    "%" => ast::BinaryOperator::Mod,
};

LogicalOperator1: ast::BinaryOperator = {
    "<" => ast::BinaryOperator::Less,
    "<=" => ast::BinaryOperator::LessEqual,
    ">" => ast::BinaryOperator::Greater,
    ">=" => ast::BinaryOperator::GreaterEqual,
};

LogicalOperator2: ast::BinaryOperator = {
    "==" => ast::BinaryOperator::Equal,
    "===" => ast::BinaryOperator::StrictEqual,
    "!=" => ast::BinaryOperator::NotEqual,
    "!==" => ast::BinaryOperator::StrictNotEqual,
};

LogicalOperator3: ast::BinaryOperator = {
    "&&" => ast::BinaryOperator::And,
};

LogicalOperator4: ast::BinaryOperator = {
    "||" => ast::BinaryOperator::Or,
};

VariableDefinition: ast::VariableDefinition<'input> = {
    <identifier:Identifier> ":" <kind:VariableKind> => ast::VariableDefinition {
        identifier,
        kind: Some(kind),
        is_writable: true,
    },
}

VariableDefinitionOptionalKind: ast::VariableDefinition<'input> = {
    VariableDefinition,
    <identifier:Identifier> => ast::VariableDefinition {
        identifier,
        kind: None,
        is_writable: true,
    },
}

VariableIdentifier: ast::VariableIdentifier<'input> = {
    <identifier:Identifier> => ast::VariableIdentifier::Identifier(identifier),
    <base:VariableIdentifier> "." <property:Identifier> => ast::VariableIdentifier::Property {
        base: Box::new(base),
        property,
    },
    <base:VariableIdentifier> "[" <index:Expression> "]" => ast::VariableIdentifier::Index { base: Box::new(base), index: Box::new(index) },
};

Identifier: &'input str =
    <name:r"[a-zA-Z][a-zA-Z0-9]*"> => name;

String: ast::Constant<'input> = {
    <value:r"'[^\n\r']*'"> => ast::Constant::String(&<>[1..<>.len()-1]),
    <value:r"`[^\n\r`]*`"> => ast::Constant::String(&<>[1..<>.len()-1]),
};

Number: ast::Constant<'input> = {
    <value:r"[0-9]+"> => ast::Constant::Integer(u64::from_str(<>).unwrap()),
    <value:r"[0-9]+\.[0-9]+"> => ast::Constant::Float(f64::from_str(<>).unwrap()),
}

Constant: ast::Constant<'input> = {
    Number,
    String,
    "true" => ast::Constant::Boolean(true),
    "false" => ast::Constant::Boolean(false),
    "undefined" => ast::Constant::Undefined,
    "null" => ast::Constant::Null,
};

VariableKind: ast::VariableKind = {
    "string" => ast::VariableKind::String,
    "number" => ast::VariableKind::Number,
};
