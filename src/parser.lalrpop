grammar;

use std::str::FromStr;
use indexmap::IndexSet;
use indexmap::IndexMap;

use crate::ast;

match {
    ",",
    ".",
    ";",
    ":",
    "=",
    "+",
    "-",
    "!",
    "*",
    "/",
    "%",
    "==",
    "===",
    "!=",
    "!==",
    "<",
    "<=",
    ">",
    ">=",
    "&&",
    "||",
    "{",
    "}",
    "(",
    ")",
    "[",
    "]",
    "?",

    "...",
    "let",
    "const",
    "any",
    "string",
    "number",

    "true",
    "false",
    "null",
    "undefined",

    "typeof",
    "class",
    "new",
    "return",
    "void",
    "declare",
    "function",
    "export",
    "import",
    "from",

    r"[0-9]+",
    r"[0-9]+\.[0-9]+",
    r"[a-zA-Z_][a-zA-Z0-9_]*",
    r"@[a-zA-Z_][a-zA-Z0-9_]*",
    r"'[^\n\r']*'",
    r"`[^\n\r`]*`",

    // Skip whitespace and comments
    r"\s*" => { },
    r"//[^\n\r]*[\n\r]*" => { }, // `// comment`
    r"/\*([^\*]*\*+[^\*/])*([^\*]*\*+|[^\*])*\*/" => { }, // `/* comment */`
}

CommaList<T>: Vec<T> = {
    <items: (<T> ",")*> <last: T?> => {
        let mut items = items;
        items.extend(last);
        items
    }
};

pub Program: ast::Program<'input> =
    <statements:StatementList> => ast::Program {
        statements,
    };

StatementList = Statement*;

Body = "{" <StatementList> "}";

Statement: ast::Statement<'input> = {
    <expression:Expression> ";" => ast::Statement::ExpressionStatement { expression },
    ";" => ast::Statement::EmptyStatement,
    DefinitionStatement,
    FunctionStatement,
    ReturnStatement,
    ClassStatement,
};

ClassFunctionStatement: ast::Statement<'input> = {
    <l1:@L> <decorators:FunctionDecorators> <ll1:@L> <name:IdentifierName> <ll2:@R> "(" <parameters:CommaList<FunctionParameter>> ")" <return_kind:(":" <FunctionReturnKind>)?> <l2:@R> <statements:Body> => {
        let this_definition = ast::VariableDefinition {
            location: (l1, l2),
            name,
            kind: ast::VariableKind::Any,
            is_writable: false,
            is_external: false,
            decorators: IndexSet::new(),
        };
        let this_param_kind = ast::ParameterKind {
            sub_kind: ast::VariableKind::Any,
            is_optional: false,
            is_rest: false,
        };
        let mut parameters = parameters;
        parameters.insert(0, (this_definition, this_param_kind));

        ast::Statement::FunctionStatement {
            location: (l1, l2),
            is_class_method: true,
            definition: ast::VariableDefinition {
                location: (ll1, ll2),
                name,
                kind: ast::VariableKind::Function {
                    return_kind: Box::new(return_kind.unwrap_or(ast::VariableKind::Any)),
                    parameters: parameters
                        .iter()
                        .map(|(_, p)| p.clone())
                        .collect(),
                },
                is_writable: false,
                is_external: false,
                decorators,
            },
            parameters: parameters
                .iter()
                .map(|(p, _)| p.clone())
                .collect(),
            statements: Vec::new(),
        }
    },
};

ClassStatement: ast::Statement<'input> = {
    <l1:@L> "class" <ll1:@L> <name:IdentifierName> <ll2:@R> <l2:@R> "{" <body:ClassFunctionStatement*> "}" => ast::Statement::ClassStatement {
        location: (l1, l2),
        definition: ast::VariableDefinition {
            location: (ll1, ll2),
            name,
            kind: ast::VariableKind::Class,
            is_writable: false,
            is_external: false,
            decorators: IndexSet::new(),
        },
        statements: body,
    }
};

FunctionReturnKind: ast::VariableKind = {
    "void" => ast::VariableKind::Undefined,
    VariableKind,
};

FunctionDecorators: IndexSet<&'input str> = {
    <names:DecoratorName*> => {
        IndexSet::from_iter(names.iter().map(|s| &s[1..]))
    }
};

FunctionParameter: (ast::VariableDefinition<'input>, ast::ParameterKind) = {
    <l1:@L> <rest:"..."?> <name:IdentifierName> <optional:"?"?> <kind:(":" <VariableKind>)?> <l2:@R> => {
        let kind = kind.unwrap_or(ast::VariableKind::Any);

        let definition = ast::VariableDefinition {
            location: (l1, l2),
            name,
            kind: kind.clone(),
            is_writable: true,
            is_external: false,
            decorators: IndexSet::new(),
        };

        let param_kind = ast::ParameterKind {
            sub_kind: kind,
            is_optional: optional.is_some(),
            is_rest: rest.is_some(),
        };

        (definition, param_kind)
    }
}

FunctionStatement: ast::Statement<'input> = {
    <l1:@L> <decorators:FunctionDecorators> "declare" "function" <ll1:@L> <name:IdentifierName> <ll2:@R> "(" <parameters:CommaList<FunctionParameter>> ")" <return_kind:(":" <FunctionReturnKind>)?> ";" <l2:@R> => ast::Statement::FunctionStatement {
        location: (l1, l2),
        is_class_method: false,
        definition: ast::VariableDefinition {
            location: (ll1, ll2),
            name,
            kind: ast::VariableKind::Function {
                return_kind: Box::new(return_kind.unwrap_or(ast::VariableKind::Any)),
                parameters: parameters
                    .iter()
                    .map(|(_, p)| p.clone())
                    .collect(),
            },
            is_writable: false,
            is_external: true,
            decorators,
        },
        parameters: parameters
            .iter()
            .map(|(p, _)| p.clone())
            .collect(),
        statements: Vec::new(),
    },

    <l1:@L> <decorators:FunctionDecorators> "function" <ll1:@L> <name:IdentifierName> <ll2:@R> "(" <parameters:CommaList<FunctionParameter>> ")" <return_kind:(":" <FunctionReturnKind>)?> <l2:@R> <statements:Body> => ast::Statement::FunctionStatement {
        location: (l1, l2),
        is_class_method: false,
        definition: ast::VariableDefinition {
            location: (ll1, ll2),
            name,
            kind: ast::VariableKind::Function {
                return_kind: Box::new(return_kind.unwrap_or(ast::VariableKind::Any)),
                parameters: parameters
                    .iter()
                    .map(|(_, p)| p.clone())
                    .collect(),
            },
            is_writable: false,
            is_external: false,
            decorators,
        },
        parameters: parameters
            .iter()
            .map(|(p, _)| p.clone())
            .collect(),
        statements,
    },
};

ReturnStatement: ast::Statement<'input> = {
    <l1:@L> "return" <e:Expression?> ";" <l2:@R> => ast::Statement::ReturnStatement {
        location: (l1, l2),
        expression: e,
    }
}

DefinitionStatementPrefix = {
    "let",
    "const",
};

DefinitionStatement: ast::Statement<'input> = {
    <l1:@L> <prefix:DefinitionStatementPrefix> <definition:VariableDefinition> <e:("=" <Expression>)?> ";" <l2:@R> => {
        let mut definition = definition;
        definition.is_writable = prefix == "let";

        ast::Statement::DefinitionStatement {
            location: (l1, l2),
            definition,
            expression: e,
        }
    },
}

Expression: ast::Expression<'input> = {
    NewExpression,
    AssignmentExpression,
    ArrayExpression,
    ObjectExpression,
    TypeOfExpression,
    BinaryExpression,
};

PropertyDefinition: (&'input str, ast::Expression<'input>) = {
    <name:IdentifierName> ":" <e:Expression> => (name, e),
};

ObjectExpression: ast::Expression<'input> =
    <l1:@L> "{" <properties:CommaList<PropertyDefinition>> "}" <l2:@R> => ast::Expression::ObjectExpression {
        location: (l1, l2),
        properties: properties.into_iter().collect::<IndexMap<_, _>>(),
    };

NewExpression: ast::Expression<'input> =
    <l1:@L> "new" <identifier:VariableIdentifier> <l2:@R> "(" <arguments:CommaList<Expression>> ")" => ast::Expression::NewExpression {
        location: (l1, l2),
        identifier,
        arguments,
    };

TypeOfExpression: ast::Expression<'input> =
    <l1:@L> "typeof" <e:Expression> <l2:@R> => ast::Expression::TypeOfExpression {
        location: (l1, l2),
        expression: Box::new(e),
    };

AssignmentExpression: ast::Expression<'input> =
    <l1:@L> <identifier:VariableIdentifier> "=" <e:Expression> <l2:@R> => ast::Expression::AssignmentExpression {
        location: (l1, l2),
        identifier,
        expression: Box::new(e),
    };

ArrayExpression: ast::Expression<'input> =
    <l1:@L> "[" <items:CommaList<Expression>> "]" <l2:@R> => ast::Expression::ArrayExpression {
        location: (l1, l2),
        items,
    };

Term<S, O, E>: ast::Expression<'input> = {
    <l1:@L> <left:S> <operator:O> <right:E> <l2:@R> => ast::Expression::BinaryExpression {
        location: (l1, l2),
        left: Box::new(left),
        operator,
        right: Box::new(right),
    },
    E,
};

BinaryExpression = LogicalTerm4;

LogicalTerm4 = Term<LogicalTerm4, LogicalOperator4, LogicalTerm3>;
LogicalTerm3 = Term<LogicalTerm3, LogicalOperator3, LogicalTerm2>;
LogicalTerm2 = Term<LogicalTerm2, LogicalOperator2, LogicalTerm1>;
LogicalTerm1 = Term<LogicalTerm1, LogicalOperator1, AddTerm>;
AddTerm = Term<AddTerm, AddOperator, MulTerm>;
MulTerm = Term<MulTerm, MulOperator, UnaryTerm>;

UnaryTerm: ast::Expression<'input> = {
    <l1:@L> <operator:UnaryOperator> <e:UnaryTerm> <l2:@R> => ast::Expression::UnaryExpression {
        location: (l1, l2),
        operator,
        expression: Box::new(e),
    },
    Factor,
};

Factor: ast::Expression<'input> = {
    <l1:@L> <value:Constant> <l2:@R> => ast::Expression::ConstantExpression {
        location: (l1, l2),
        value,
    },
    <l1:@L> <identifier:VariableIdentifier> <l2:@R> => ast::Expression::VariableExpression {
        location: (l1, l2),
        identifier
    },
    <l1:@L> <identifier:VariableIdentifier> "(" <arguments:CommaList<Expression>> ")" <l2:@R> => ast::Expression::CallExpression {
        location: (l1, l2),
        identifier,
        arguments
    },
    "(" <e:Expression> ")" => e,
};

UnaryOperator: ast::UnaryOperator = {
    "+" => ast::UnaryOperator::Positive,
    "-" => ast::UnaryOperator::Negative,
    "!" => ast::UnaryOperator::Not,
};

AddOperator: ast::BinaryOperator = {
    "+" => ast::BinaryOperator::Addition,
    "-" => ast::BinaryOperator::Subtraction,
};

MulOperator: ast::BinaryOperator = {
    "*" => ast::BinaryOperator::Multiplication,
    "/" => ast::BinaryOperator::Division,
    "%" => ast::BinaryOperator::Mod,
};

LogicalOperator1: ast::BinaryOperator = {
    "<" => ast::BinaryOperator::Less,
    "<=" => ast::BinaryOperator::LessEqual,
    ">" => ast::BinaryOperator::Greater,
    ">=" => ast::BinaryOperator::GreaterEqual,
};

LogicalOperator2: ast::BinaryOperator = {
    "==" => ast::BinaryOperator::Equal,
    "===" => ast::BinaryOperator::StrictEqual,
    "!=" => ast::BinaryOperator::NotEqual,
    "!==" => ast::BinaryOperator::StrictNotEqual,
};

LogicalOperator3: ast::BinaryOperator = {
    "&&" => ast::BinaryOperator::And,
};

LogicalOperator4: ast::BinaryOperator = {
    "||" => ast::BinaryOperator::Or,
};

VariableDefinition: ast::VariableDefinition<'input> = {
    <l1:@L> <name:IdentifierName> <kind:(":" <VariableKind>)?> <l2:@R> => ast::VariableDefinition {
        location: (l1, l2),
        name,
        kind: kind.unwrap_or(ast::VariableKind::Any),
        is_writable: true,
        is_external: false,
        decorators: IndexSet::new(),
    },
}

VariableIdentifier: ast::VariableIdentifier<'input> = {
    <l1:@L> <name:IdentifierName> <l2:@R> => ast::VariableIdentifier::Name {
        location: (l1, l2),
        name,
    },
    <l1:@L> <base:VariableIdentifier> "." <property:IdentifierName> <l2:@R> => ast::VariableIdentifier::Property {
        location: (l1, l2),
        base: Box::new(base),
        property,
    },
    <l1:@L> <base:VariableIdentifier> "[" <index:Expression> "]" <l2:@R> => ast::VariableIdentifier::Index {
        location: (l1, l2),
        base: Box::new(base),
        index: Box::new(index),
    },
};

DecoratorName: &'input str =
    <name:r"@[a-zA-Z_][a-zA-Z0-9_]*"> => name;

IdentifierName: &'input str =
    <name:r"[a-zA-Z_][a-zA-Z0-9_]*"> => name;

String: ast::Constant<'input> = {
    <value:r"'[^\n\r']*'"> => ast::Constant::String(&<>[1..<>.len()-1]),
    <value:r"`[^\n\r`]*`"> => ast::Constant::String(&<>[1..<>.len()-1]),
};

Number: ast::Constant<'input> = {
    <value:r"[0-9]+"> => ast::Constant::Integer(u64::from_str(<>).unwrap()),
    <value:r"[0-9]+\.[0-9]+"> => ast::Constant::Float(f64::from_str(<>).unwrap()),
}

Constant: ast::Constant<'input> = {
    Number,
    String,
    "true" => ast::Constant::Boolean(true),
    "false" => ast::Constant::Boolean(false),
    "null" => ast::Constant::Null,
    "undefined" => ast::Constant::Undefined,
};

VariableKind: ast::VariableKind = {
    "any" => ast::VariableKind::Any,
    "string" => ast::VariableKind::String,
    "number" => ast::VariableKind::Number,
    <v:VariableKind> "[" "]" => ast::VariableKind::Array {
        kind: Box::new(v),
    },
};
